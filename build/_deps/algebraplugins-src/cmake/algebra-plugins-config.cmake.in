# Algebra plugins library, part of the ACTS project (R&D line)
#
# (c) 2021-2023 CERN for the benefit of the ACTS project
#
# Mozilla Public License Version 2.0

# Set up the helper functions/macros.
@PACKAGE_INIT@

# Set up variables describing which plugins were enabled during the Algebra
# Plugins build.
set( ALGEBRA_PLUGINS_INCLUDE_EIGEN @ALGEBRA_PLUGINS_INCLUDE_EIGEN@ )
set( ALGEBRA_PLUGINS_INCLUDE_SMATRIX @ALGEBRA_PLUGINS_INCLUDE_SMATRIX@ )
set( ALGEBRA_PLUGINS_INCLUDE_VC @ALGEBRA_PLUGINS_INCLUDE_VC@ )
set( ALGEBRA_PLUGINS_INCLUDE_VECMEM @ALGEBRA_PLUGINS_INCLUDE_VECMEM@ )

# Set up some simple variables for using the package.
set( algebra_plugins_VERSION "@PROJECT_VERSION@" )
set_and_check( algebra_plugins_INCLUDE_DIR
   "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@" )
set_and_check( algebra_plugins_CMAKE_DIR "@PACKAGE_CMAKE_INSTALL_CMAKEDIR@" )

# Find all packages that Algebra Plugins needs to function.
include( CMakeFindDependencyMacro )
if( ALGEBRA_PLUGINS_INCLUDE_EIGEN )
   find_dependency( Eigen3 )
endif()
if( ALGEBRA_PLUGINS_INCLUDE_SMATRIX )
   find_dependency( ROOT COMPONENTS Smatrix )
endif()
if( ALGEBRA_PLUGINS_INCLUDE_VC )
   find_dependency( Vc 1.4.2 )
endif()
if( ALGEBRA_PLUGINS_INCLUDE_VECMEM )
   find_dependency( vecmem )
endif()

# Print a standard information message about the package being found.
include( FindPackageHandleStandardArgs )
find_package_handle_standard_args( algebra-plugins REQUIRED_VARS
   CMAKE_CURRENT_LIST_FILE
   VERSION_VAR algebra_plugins_VERSION )

# Include the file listing all the imported targets and options.
include( "${algebra_plugins_CMAKE_DIR}/algebra-plugins-config-targets.cmake" )
