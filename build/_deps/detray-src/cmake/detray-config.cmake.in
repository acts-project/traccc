# Detray library, part of the ACTS project (R&D line)
#
# (c) 2021-2024 CERN for the benefit of the ACTS project
#
# Mozilla Public License Version 2.0

# Set up the helper functions/macros.
@PACKAGE_INIT@

# Set up variables describing which components were enabled during the
# Detray build.
set( DETRAY_EIGEN_PLUGIN @DETRAY_EIGEN_PLUGIN@ )
set( DETRAY_SMATRIX_PLUGIN @DETRAY_SMATRIX_PLUGIN@ )
set( DETRAY_VC_AOS_PLUGIN @DETRAY_VC_AOS_PLUGIN@ )
set( DETRAY_VC_SOA_PLUGIN @DETRAY_VC_SOA_PLUGIN@ )
set( DETRAY_DISPLAY @DETRAY_DISPLAY@ )

# Set up some simple variables for using the package.
set( detray_VERSION "@PROJECT_VERSION@" )
set_and_check( detray_INCLUDE_DIR
   "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@" )
set_and_check( detray_CMAKE_DIR "@PACKAGE_CMAKE_INSTALL_CMAKEDIR@" )

# Find all packages that Detray needs to function.
include( CMakeFindDependencyMacro )
find_dependency( algebra-plugins )
find_dependency( covfie )
find_dependency( vecmem )
find_dependency( dfelibs )
find_dependency( nlohmann_json )
if( DETRAY_DISPLAY )
   find_dependency( actsvg )
endif()

# Print a standard information message about the package being found.
include( FindPackageHandleStandardArgs )
find_package_handle_standard_args( detray REQUIRED_VARS
   CMAKE_CURRENT_LIST_FILE
   VERSION_VAR detray_VERSION )

# Include the file listing all the imported targets and options.
include( "${detray_CMAKE_DIR}/detray-config-targets.cmake" )
